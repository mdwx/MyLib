apply plugin: 'com.android.library'
apply plugin: 'checkstyle'
apply plugin: 'com.novoda.bintray-release'

check.dependsOn('checkstyle')

task checkstyle(type: Checkstyle) {
  configFile = rootProject.file('checkstyle.xml')
  source 'src'
  include '**/*.java'
  exclude '**/gen/**'

  classpath = files()
}

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.3"

  defaultConfig {
    minSdkVersion 19
    targetSdkVersion 23
  }

  lintOptions {
    abortOnError false
  }
}

// build a jar with source files
task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}

task javadoc(type: Javadoc) {
  failOnError false
  source = android.sourceSets.main.java.sourceFiles
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

dependencies {
  testCompile 'junit:junit:4.12'
  testCompile "org.robolectric:robolectric:3.0"
  testCompile 'org.assertj:assertj-core:1.7.0'
}

publish {
  userOrg = rootProject.userOrg
  groupId = rootProject.groupId
  artifactId = rootProject.artifactId
  publishVersion = rootProject.publishVersion
  desc = rootProject.description
  website = rootProject.website
  licences = rootProject.licences
}